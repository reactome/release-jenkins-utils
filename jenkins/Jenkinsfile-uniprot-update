// This Jenkinsfile is used by Jenkins to run the 'UniProt Update' step of Reactome's release.
// This step synchronizes Reactome's database with UniProt.
 
import groovy.json.JsonSlurper
import org.reactome.release.jenkins.utilities.Utilities

// Shared library maintained at 'release-jenkins-utils' repository.
def utils = new Utilities()

pipeline {
	agent any

	stages {
		// This stage checks that an upstream step, ConfirmReleaseConfigs, was run successfully.
		stage('Check ConfirmReleaseConfigs build succeeded'){
			steps{
				script{
					utils.checkUpstreamBuildsSucceeded("ConfirmReleaseConfigs")
				}
			}
		}
		// This stage backs up the gk_central database before it is modified.
		stage('Setup: Back up gk_central before modifications'){
			steps{
				script{
					withCredentials([usernamePassword(credentialsId: 'mySQLCuratorUsernamePassword', passwordVariable: 'pass', usernameVariable: 'user')]){
					    utils.takeDatabaseDumpAndGzip("${env.GK_CENTRAL_DB}", "uniprot_update", "before", "${env.CURATOR_SERVER}")
					}
				}
			}
		}
		// Download uniprot_sprot.xml.gz and uniprot-reviewed:no.list.gz from UniProt.
		stage('Setup: Download uniprot_sprot.xml.gz and uniprot-reviewed:no.list.gz'){
			steps{
				script{
					sh "wget -q -N ftp://ftp.uniprot.org/pub/databases/uniprot/current_release/knowledgebase/complete/uniprot_sprot.xml.gz"
					sh "wget -q -O uniprot-reviewed:no.list.gz 'http://www.uniprot.org/uniprot/?query=reviewed%3Ano&compress=yes&format=list'"
					sh "mv uniprot*gz ${env.ABS_RELEASE_PATH}/uniprot_update/"
				}
			}
		}
		// This stage executes the UniProt Update Perl script. 
		stage('Main: UniProt Update'){
			steps {
				script{
					dir("${env.ABS_RELEASE_PATH}/uniprot_update/") {
						withCredentials([usernamePassword(credentialsId: 'mySQLCuratorUsernamePassword', passwordVariable: 'pass', usernameVariable: 'user')]){
							sh "perl uniprot_xml2sql_isoform.pl -db ${env.GK_CENTRAL_DB} -host ${env.CURATOR_SERVER} -user $user -pass $pass > uniprot.out 2> uniprot.err"
						}
					}
				}
			}
		}
		// This stage backs up the gk_central database after modification.
		stage('Post: Backup gk_central after modifications'){
			steps{
				script{
					withCredentials([usernamePassword(credentialsId: 'mySQLCuratorUsernamePassword', passwordVariable: 'pass', usernameVariable: 'user')]){
						utils.takeDatabaseDumpAndGzip("${env.GK_CENTRAL_DB}", "uniprot_update", "after", "$user", "$pass", "${env.CURATOR_SERVER}")
					}
				}
			}
		}
		// This stage emails the uniprot.wiki file to the default recipients list.
		stage('Post: Email UniProt.wiki file'){
			steps{
				script{
					// Email attachments need to take place within Jenkins' working directory, so the uniprot.wiki file needs to be moved from uniprot_update (in old Release directory)
					def uniprotWikiFile = "uniprot.wiki"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/${uniprotWikiFile} ."
					
					def releaseVersion = utils.getReleaseVersion();
					def emailSubject = "UniProt Update Reports for v${releaseVersion}"
					def emailBody = "Hello,\n\nThis is an automated message from Jenkins regarding an update for v${releaseVersion}. The UniProt Update step has completed. Please review the ${uniprotWikiFile} file attached to this email. If it looks correct, the contents of the file need to be uploaded to https://devwiki.reactome.org/index.php/Reports_Archive under 'UniProt Update Reports'. Please add the current UniProt wiki URL to the 'Archived reports' section of the page. If the file looks incorrect, please email the developer running Release. \n\nThanks!"
					utils.sendEmailWithAttachment("$emailSubject", "$emailBody", "$uniprotWikiFile")
				}
			}
		}
		// All databases, logs, and data files generated by this step are compressed before moving them to the Reactome S3 bucket. 
		// All files are then deleted.
		stage('Post: Archive Outputs'){
			steps{
				script{
				    def releaseVersion = utils.getReleaseVersion();
					def s3Path = "${env.S3_RELEASE_DIRECTORY_URL}/${releaseVersion}/uniprot_update"
					sh "mkdir -p databases/ data/ logs/"
					
					sh "mv --backup=numbered *_${releaseVersion}_*.dump.gz databases/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/sequence_uniprot_report.txt data/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/reference_DNA_sequence_report.txt data/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/duplicated_db_id.txt data/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/trembl_to_update.acc data/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/uniprot-reviewed:no.list data/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/uniprot_sprot.xml data/"
					sh "mv uniprot.wiki data/"
					sh "mv ${env.ABS_RELEASE_PATH}/uniprot_update/uniprot.* logs/"
					
					sh "gzip -r data/* logs/*"
					sh "aws s3 --no-progress --recursive cp databases/ $s3Path/databases/"
					sh "aws s3 --no-progress --recursive cp logs/ $s3Path/logs/"
					sh "aws s3 --no-progress --recursive cp data/ $s3Path/data/"
					sh "rm -r databases logs data"
				}
			}
		}
	}
}
